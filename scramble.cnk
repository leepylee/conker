
func main()
  str[] shapes
  num[] fuels
  num[] heights = calcLand()

  num s = landShape() // shape for land
  console false

  num b = fuelBitmap()

  num x
  num fuelsIndex
  num hindex

  // fps
  num timeOld = now()
  num frame
  num totalTime    
end

func num[] calcLand()
  num[] h
  num height = 0
  num nextFuel = 100 + rnd(100)
  for x to 3000
    // fuel dump calcs
    nextFuel--
    bool goUp
    if x < 30
      goUp = true
    end
    if rnd(10) > 4
      goUp = true
    end
    if height > 300
      goUp = false
    end
    if nextFuel <= 0
      if nextFuel == 0
        add fuels, x
      end
      num updown = nextFuel - (int(nextFuel / 8)*8)
      // gentle flatish surface under fuel dump
      if updown == 0
        height++
      else if updown == 4
        height--
      end
      if nextFuel == -15
        nextFuel = 100 + rnd(100)
        if rnd(2) == 0
          nextFuel = 1 // ocassional have an immediate next fuel dump
        end
      end
    else if goUp
      height++
    else if height > 30
      height--
    end
    num top = SCREENH - height * 5
    add h, top
  end
  return h
end

func num landShape()
  for x to length(heights)
    num top = heights[x]
    num pos = x * 5
    format sh, 'K{0} R{1} {2} {3} {4}', RED, pos, top, pos + 5, SCREENH
    add shapes, sh
    format sh2, 'K{0} R{1} {2} {3} {4}', WHITE, pos, top, pos + 5, top - 5
    add shapes, sh2
  end
  str shapesAll = join(shapes ' ')
  shape s, shapesAll
  return s
end

func num fuelBitmap()
  str s = '    ggggggg    ' + _
          '  ggggggggggg  ' + _
          ' gppprgpppprgg ' + _
          'ggpggrgrpggrggg' + _
          'ggppgrgrppgrggg' + _
          'ggpggrgrpggrggg' + _
          ' gpggrrrppprrg ' + _
          '  ggggggggggg  ' + _
          '    ggggggg    ' + _
          '     ppppp     ' + _
          '    p     p    ' + _
          '    ppppppp    ' + _
          '   p       p   ' + _
          '   ppppppppp   ' + _
          '  p         p  ' + _
          ' ppp       ppp '
  dict num pallete
  put pallete, 'g', GREEN
  put pallete, 'p', DEEPPINK
  put pallete, 'r', RED
  picture b, s, 15, pallete
  return b
end

func draw()
  style FILL

  // draw land
  drawshape s, -x, 0

  // draw fuel dumps
  num i = fuelsIndex
  while i < length(fuels)
    num fuelx = fuels[i]
    num diff = fuelx - hindex
    if diff < -16
      fuelsIndex++
    else if diff < 400
      drawbitmap b, diff * 5, heights[fuelx] - 80 -5, 75, 80
    else
      break 
    end
    i++
  end

  // move camera
  x+=5
  hindex++

  // loop world 
  if x > 2600 * 5
    x = 0
    fuelsIndex = 0
    hindex = 0
  end

  drawFps()
end  

func drawFps()
  frame++
  num timeNow = now()
  num dt = timeNow - timeOld
  if dt == 0
    dt = 1
  end
  num fps = 1000 / dt
  totalTime += fps
  if frame > 10
    textsize 50
    paint RED
    num avg = round(totalTime/frame)
    text MIDX, MIDY, string(avg)
  end
  timeOld = timeNow
end
