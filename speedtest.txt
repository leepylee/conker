func main()
  // make choice
  num qty
  num button
  num listItem
  num pixlen
  num pathQty
  num shapeQty
  bool isMoving
  doDialog()
  if button == 1
    stop
  end
  console false

  // bitmap
  bitmap b, 16, 16
  canvasmap b
  line 0,0,15,15
  line 0,15,15,0

  // path
  num p
  setupPath()

  num s
  setupShape()

  num px

  button = -1 // stop setup

  // fps
  num timeOld = now()
  num frame
  num totalTime
    
end

func setupPath()
  if pathQty > 0
    style FILLSTROKE
    str[] elements
    for i to qty
      num x = rnd(SCREENW)
      num y = rnd(SCREENH)
      num x2 = x + pixlen
      num y2 = y + pixlen
      str form
      if listItem == 0
        form = 'C{0} {1} {2} 0'
        x2 = pixlen
      else if listItem == 1
        form = 'R{0} {1} {2} {3} 0'
      else if listItem == 2
        form = 'M{0} {1} L{2} {3} Z'
      else if listItem == 4
        form = 'M{0} {1} L{2} {3} L{4} {5} Z'
      end
      format s, form, x, y, x2, y2, x, y + pixlen
      add elements, s
    end
    str sp = join(elements, ' ')
    path p1, sp
    p = p1
  end
end

func setupShape()
  if shapeQty > 0
    str[] elements
    for i to qty
      num x = rnd(SCREENW)
      num y = rnd(SCREENH)
      num x2 = x + pixlen
      num y2 = y + pixlen
      str form
      if listItem == 0
        form = 'C{0} {1} {2}'
        x2 = pixlen
      else if listItem == 1
        form = 'R{0} {1} {2} {3}'
      else if listItem == 2
        form = 'L{0} {1} {2} {3}'
      else if listItem == 4
        form = 'Y3 {0} {1} {2} {3} {4} {5}'
      end
      format s, form, x, y, x2, y2, x, y + pixlen
      add elements, s
    end
    str sp = join(elements, ' ')
    shape s1, sp
    s = s1
  end
end

func draw()
  if button == 1
    stop
  else if button == 0
    setupPath()
    setupShape()
    button = -1
    timeOld = now()
    frame = 0
    totalTime = 0
  end
  
  if inkey(0) != ''
    doDialog()
  end
  
  if isMoving
    px++
  end

  style FILL
  if pathQty > 0
    for i to pathQty
      drawpath p, px + i*20, i*20
    end
  end

  if shapeQty > 0
    for i to shapeQty
      drawshape s, px - i*20, i*20
    end
  end

  if pathQty == 0 and shapeQty == 0
    for i to qty
      paint 255, rnd(256), rnd(256), rnd(256)
      num x = rnd(SCREENW)
      num y = rnd(SCREENH)
      if listItem == 0
        circle x, y, pixlen
      else if listItem == 1
        rect x, y, x + pixlen, y + pixlen
      else if listItem == 2
        line x, y, x + pixlen, y + pixlen
      else if listItem == 3
        drawbitmap b, x, y, 16 * pixlen, 16 * pixlen
      else if listItem == 4
        polygon x, y, x + pixlen, y, x, y+ pixlen
      end 
    end
  end

  // fps
  frame++
  num timeNow = now()
  num fps = 1000 / (timeNow - timeOld)
  totalTime += fps
  if frame > 10
    textsize 50
    paint RED
    num avg = round(totalTime/frame)
    text 200, MIDY, 'fps ' + avg + ' qty ' + qty + ' item ' + listItem + ' length ' + pixlen + ' paths ' + pathQty + ' shapes ' + shapeQty + ' moving ' + isMoving
  end
  timeOld = timeNow
end  

func doDialog()
  px = 0
  dialog 'Graphic Speed Test', 'Quantity', qty, listItem, 'Circles;Rectangles;Lines;Bitmaps;Triangles', 'Pixel length', pixlen, 'Paths', pathQty, 'Shapes', shapeQty, 'Moving Path/Shape', isMoving, button, 'OK;Cancel'
end
