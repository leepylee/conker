func main()
  num listItem
  str[] fns = {'Colour', 'Linear', 'Radial', 'Sweep', 'Stroke', 'Reset'}
  choose()
  
  // strokedash {pixels-on, pixels-off ...}, pixels-phase-offset
  // this style is remembered in the path
  num[] arr = {100,5} 
  strokedash arr, 10
  strokewidth 10
  path p,'M0 0 L400 50 L-100 400 Z'

  num f 
  num c1
  num c2
  num px
  num py
end

func draw()
  str fn = 'draw' + fns[listItem]
  sendevent(fn, '')
  if inkey(0) == ' '
    choose()
  end
  f++
end

func choose()
  str choices = join(fns, ';')
  dialog 'Choose example', listItem, choices
end

func str drawColour(str msg) event
  if f % 100 == 0
    c1 = randcol()
    c2 = randcol() 
    px = rnd(SCREENW)
    py = rnd(SCREENH)
  end 
  style FILL
  canvas c1
  paint c2
  poly(px, py)
  return ''
end

funct num randcol()
  return col(255, rnd(255), rnd(255), rnd(255))
end

func str drawLinear(str msg) event
  // Linear Gradient Example
  style FILL
  // lgradient left, top, right, bottom, colour-from, colour-to , CLAMP / MIRROR / REPEAT
  lgradient 200, 0, 400, 0, TOMATO, DARKRED, CLAMP
  circle 300, 300, 100
  lgradient 0, 420, 0, 520, LIME, DARKGREEN, MIRROR
  circle 520, 520, 100
  lgradient 640, 640, 740, 740, CYAN, DARKBLUE, REPEAT
  circle 740,740,100
  return ''
end

func str drawStroke(str msg) event
  // StrokeDash Example
  num[] arr2 = {100,10,50,10,25,5} 
  strokedash arr2, 10
  strokewidth 10
  circle 500,1000,100
  drawpath p, 500,1150 // remembers original style

  line 100,200,100,300

  strokecap ROUND
  line 200,200,200,300

  strokecap SQUARE
  line 300,200,300,300

  poly(600, 150)

  strokejoin BEVEL
  poly(600, 400)

  strokejoin ROUND
  poly(600, 650)

  style FILL
  poly(900, 650)

  style FILLSTROKE
  poly(900, 650)

  return ''
end  

func poly(num x, num y)
  polygon x-100, y, x+100, y, x+150, y+125, x, y+200, x-150, y+125
end

func str drawRadial(str msg) event
  // Radial Gradient Example
  style FILL
  // rgradient x, y, radius, from-color, to-color, CLAMP / MIRROR / REPEAT
  rgradient 300, 300, 50, TOMATO, DARKRED, CLAMP
  circle 300, 300, 100
  rgradient 520, 520, 50, LIME, DARKGREEN,MIRROR
  circle 520, 520, 100
  rgradient 740, 740, 50, CYAN, DARKBLUE, REPEAT
  circle 740, 740, 100
  return ''
end  

func str drawSweep(str msg) event
  // Sweep Gradient Example
  style FILL
  // sgradient x, y, angle, from-color, to-color
  sgradient 300, 300, 90, TOMATO, DARKRED
  circle 300, 300, 100
  sgradient 520, 520, 180, LIME, DARKGREEN
  circle 520, 520, 100
  sgradient 740, 740, 270, CYAN, DARKBLUE
  circle 740, 740, 100
  return ''
end

func str drawReset(str msg) event
  // Reset Example
  paint RED 
  style FILL 
  circle MIDX, MIDY, 200
  // Reset clears all paint settings back to defaults 
  reset 
  circle SCREENW / 4, MIDY, 200
  return ''
end
  
 




